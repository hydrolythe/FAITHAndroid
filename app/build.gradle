apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'

apply plugin: 'com.google.android.gms.oss-licenses-plugin'

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
    }
}

android {
    def globalConfig = rootProject.extensions.getByName("ext")
    compileSdkVersion globalConfig["androidCompileSdkVersion"]
    defaultConfig {
        applicationId "be.hogent.faith"
        minSdkVersion globalConfig["androidMinSdkVersion"]
        targetSdkVersion globalConfig["androidTargetSdkVersion"]
        versionCode generateVersionCode()
        versionName generateVersionName()
        multiDexEnabled true
        testInstrumentationRunner "be.hogent.faith.faith.TestAppJUnitRunner"
        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "app"]
        vectorDrawables.useSupportLibrary = true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    androidExtensions {
        experimental = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        disable "ResourceType"
    }

    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildToolsVersion = '28.0.3'

    // To inline the bytecode built with JVM target 1.8 into
    // bytecode that is being built with JVM target 1.6. (e.g. navArgs)
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {
    implementation project(':domain')
    implementation project(':service')
    implementation project(':database')
    implementation project(':encryption')
    implementation project(':storage')
    implementation project(':draw')
    implementation project(':util')

    implementation authdependencies.auth0
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    def commonDependencies = rootProject.ext.commonDependencies
    def injectionDependencies = rootProject.ext.injectionDependencies
    def androidTestDependencies = rootProject.ext.androidTestDependencies
    def androidDependencies = rootProject.ext.androidDependencies
    def uiDependencies = rootProject.ext.uiDependencies

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation commonDependencies.kotlin
    implementation commonDependencies.threeTenABP
    //Use Java library for unit testing instead of Android to avoid requesting Android Device Timezone
    testImplementation(testDependencies.threeTenBP) {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
    implementation commonDependencies.rxjava
    implementation commonDependencies.rxandroid
    implementation commonDependencies.licenses
    implementation commonDependencies.timber
    implementation commonDependencies.navktx
    implementation commonDependencies.navui

    implementation injectionDependencies.koin
    implementation injectionDependencies.koin_viewModel

    implementation androidDependencies.appcompat
    implementation androidDependencies.lifecycle
    implementation androidDependencies.viewmodellifecycle

    implementation uiDependencies.glide

    implementation uiDependencies.legacysupport
    implementation uiDependencies.googleplay
    implementation uiDependencies.lottie
    implementation uiDependencies.recyclerview
    implementation uiDependencies.rv_selection
    implementation uiDependencies.constraintLayout
    implementation uiDependencies.fragment
    implementation uiDependencies.fotoApparat
    implementation uiDependencies.fotoApparatAdapters
    implementation uiDependencies.texteditor
    implementation uiDependencies.materialdrawer
    implementation uiDependencies.annotation
    implementation uiDependencies.material
    implementation uiDependencies.colorpicker
    implementation uiDependencies.viewpager2
    implementation uiDependencies.youtubePlayerView
    implementation uiDependencies.browser

    implementation firebaseDependencies.firebasestorageui

    testImplementation testDependencies.junit
    testImplementation testDependencies.mockk
    testImplementation testDependencies.coreTesting
    testImplementation testDependencies.koinTesting
    testImplementation testDependencies.hamcrest
    testImplementation testDependencies.hamcrestLibrary
    implementation testDependencies.firebase_analytics
    implementation testDependencies.crashlytics
    implementation firebaseDependencies.performance

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation androidTestDependencies.testCore
    androidTestImplementation androidTestDependencies.testRules
    // Assertions
    androidTestImplementation androidTestDependencies.androidJunit
    //espresso
    androidTestImplementation androidTestDependencies.espresso
    androidTestImplementation androidTestDependencies.espressoContrib
    //koin
    androidTestImplementation testDependencies.koinTesting
    androidTestImplementation androidTestDependencies.uiAutomator

    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    implementation 'com.jakewharton.rxbinding4:rxbinding:4.0.0'
}

private Integer generateVersionCode() {
    def globalConfig = rootProject.extensions.getByName("ext")
    return globalConfig["androidMinSdkVersion"] * 10000000 + globalConfig["versionMajor"] * 10000 + globalConfig["versionMinor"] * 100 + globalConfig["versionPatch"]
}

private String generateVersionName() {
    def globalConfig = rootProject.extensions.getByName("ext")
    String versionName = "${globalConfig["versionMajor"]}.${globalConfig["versionMinor"]}.${globalConfig["versionPatch"]}"
    return versionName
}

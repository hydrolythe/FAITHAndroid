apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
    }
}


android {
    def globalConfig = rootProject.extensions.getByName("ext")
    compileSdkVersion globalConfig["androidCompileSdkVersion"]
    defaultConfig {
        applicationId "be.hogent.faith"
        minSdkVersion globalConfig["androidMinSdkVersion"]
        targetSdkVersion globalConfig["androidTargetSdkVersion"]
        versionCode generateVersionCode()
        versionName generateVersionName()
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "app"]
        vectorDrawables.useSupportLibrary = true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        disable "ResourceType"
    }

    dataBinding {
        enabled = true
    }

    kapt {
        generateStubs = true
    }
}

dependencies {
    implementation project(':domain')
    implementation project(':service')
    implementation project(':database')
    implementation project(':storage')
    implementation project(':draw')
    implementation project(':util')


    implementation authdependencies.auth0

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def commonDependencies = rootProject.ext.commonDependencies
    def injectionDependencies = rootProject.ext.injectionDependencies
    def androidTestDependencies = rootProject.ext.androidTestDependencies
    def androidDependencies = rootProject.ext.androidDependencies
    def uiDependencies = rootProject.ext.uiDependencies

    implementation commonDependencies.kotlin
    implementation commonDependencies.threeTenABP
    implementation commonDependencies.rxjava
    implementation commonDependencies.rxandroid

    implementation commonDependencies.timber

    implementation injectionDependencies.koin
    implementation injectionDependencies.koin_viewModel

    implementation androidDependencies.appcompat
    implementation androidDependencies.lifecycle

    implementation uiDependencies.glide
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    kapt uiDependencies.glidecompiler
    implementation uiDependencies.recyclerview
    implementation uiDependencies.rv_selection
    implementation uiDependencies.constraintLayout
    implementation uiDependencies.fragment
    implementation uiDependencies.fotoApparat
    implementation uiDependencies.fotoApparatAdapters
    implementation uiDependencies.texteditor

    implementation uiDependencies.materialdrawer
    implementation uiDependencies.annotation
    implementation uiDependencies.material

    implementation commonDependencies.navktx
    implementation commonDependencies.navui

    //Use Java library for unit testing instead of Android to avoid requesting Android Device Timezone
    testImplementation(testDependencies.threeTenBP) {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }

    testImplementation testDependencies.junit
    testImplementation testDependencies.mockk
    testImplementation testDependencies.coreTesting
    testImplementation testDependencies.koinTesting

    androidTestImplementation androidTestDependencies.testCore
    androidTestImplementation androidTestDependencies.testRules
    androidTestImplementation androidTestDependencies.androidJunit
    androidTestImplementation androidTestDependencies.espresso

    androidTestImplementation androidTestDependencies.espressoContrib
    androidTestImplementation androidTestDependencies.uiAutomator

}

private Integer generateVersionCode() {
    def globalConfig = rootProject.extensions.getByName("ext")
    return globalConfig["androidMinSdkVersion"] * 10000000 + globalConfig["versionMajor"] * 10000 + globalConfig["versionMinor"] * 100 + globalConfig["versionPatch"]
}

private String generateVersionName() {
    def globalConfig = rootProject.extensions.getByName("ext")
    String versionName = "${globalConfig["versionMajor"]}.${globalConfig["versionMinor"]}.${globalConfig["versionPatch"]}"
    return versionName
}
